apply plugin: "java"

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
    maven { url "http://maven.restlet.org" }
    repositories { flatDir(dirs: "lib") }
}

configurations {
    dist
}

dependencies {
    compile "com.google.code.gson:gson:2.2.4"
    compile "org.liquibase:liquibase-core:3.0.7"
    compile "javax.mail:mail:1.4"
    compile "org.hibernate:hibernate-annotations:3.5.6-Final"
    compile "org.slf4j:slf4j-api:1.7.5"
    compile "org.restlet.jee:org.restlet:2.1.1"
    compile "org.restlet.jee:org.restlet.ext.jackson:2.1.1"
    compile "net.java.dev.jna:jna:3.2.7"
    compile "mikereedell-sunrisesunsetlib:mikereedell-sunrisesunsetlib"
    compile "org.apache.tomcat.embed:tomcat-embed-core:7.0.47"
    compile "org.apache.tomcat.embed:tomcat-embed-logging-juli:7.0.47"
    compile "org.glassfish.jersey.containers:jersey-container-servlet:2.4.1"
    runtime "postgresql:postgresql:9.1-901.jdbc4"
    runtime "org.hibernate:hibernate-core:3.6.8.Final"
    runtime "org.slf4j:slf4j-nop:1.6.1"
    runtime "javassist:javassist:3.12.0.GA" // required for hibernate
    runtime "org.glassfish.jersey.ext:jersey-bean-validation:2.4.1" // required for jersey
    testCompile "org.testng:testng:6.5.1"
    dist "ant-deb:ant-deb:0.0.1"
}

jar {
    from {
        configurations.compile.collect { project.zipTree(it) }
        configurations.runtime.collect { project.zipTree(it) }
    }

    manifest {
        attributes "Main-Class": "com.homeki.core.main.Main"
        attributes "Implementation-Version": version
    }
}

test {
    useTestNG()

    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }

    onlyIf { Boolean.getBoolean('runTests') }
}

task dist(dependsOn: jar) << {
    if (version.endsWith("-SNAPSHOT")) throw new GradleException("Can't build dist from SNAPSHOT version.")

    new File("build/dist").mkdir()

    ant.taskdef(name: "deb", classname: "com.googlecode.ant_deb_task.Deb", classpath: configurations.dist.asPath)
    ant.deb(todir: "build/dist", package: "homeki", section: "misc",  depends: "openjdk-6-jre-headless, postgresql-9.1, telldus, owfs, libjna-java", prerm: "dist/prerm",  postinst: "dist/postinst", preinst: "dist/preinst", architecture: "all", version: version) {
        description(synopsis: "Unified interface for all your sensors and devices connected to your computer.", "Provides a REST HTTP API for accessing, logging, modifying and managing various devices and sensors connected to your computer.")
        maintainer(name: "Homeki Development Team", email: "contact@homeki.com")
        tarfileset(file: "build/libs/homekicore-${version}.jar", fullpath: "/opt/homeki/homekicore.jar", filemode: "755")
        tarfileset(file: "dist/homekid", prefix: "/etc/init.d/", filemode: "755")
        tarfileset(file: "dist/start.sh", prefix: "/opt/homeki/", filemode: "755")
        tarfileset(file: "logging.properties", prefix: "/opt/homeki/", filemode: "755")
    }
}
